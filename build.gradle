buildscript {
	repositories {
		mavenCentral();
		jcenter();
		maven {
			name = "sonatype";
			url = "https://oss.sonatype.org/content/repositories/snapshots/";
		}
		maven {
			url "https://plugins.gradle.org/m2/";
		}
	}
	dependencies {
		classpath "gradle.plugin.net.minecrell:licenser:0.3";
	}
}

apply plugin: "java";
apply plugin: "net.minecrell.licenser";
apply plugin: "eclipse";
apply plugin: "maven-publish";

group = "blue.endless.jankson";
archivesBaseName = "Jankson";
version = "1.0.0";

sourceCompatibility = 1.8;
targetCompatibility = 1.8;

repositories {
	jcenter()
	mavenCentral()
}

def versionSuffix = '-SNAPSHOT'
if (System.env.BUILD_NUMBER) {
	versionSuffix = ''
	version = version+'-'+System.env.BUILD_NUMBER
}

publishing {
	publications {
		mavenPrimary(MavenPublication) {
			from components.java;

			groupId project.group;
			artifactId project.name;
			version project.version+versionSuffix;
			artifact sourcesJar;
		}
	}
}

if (file("private.gradle").exists()) {
	apply from: "private.gradle";
}

defaultTasks "clean", "build", "sourcesJar";


task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

dependencies {
	testCompile "junit:junit:4.12";
	
	compile group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2";
}
